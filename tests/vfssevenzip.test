if {[lsearch [namespace children] ::tcltest] < 0} {
    package require tcltest 2
    namespace import ::tcltest::*
}


package require sevenzip 1
package require vfs::sevenzip

catch {
    if {[info exist env(7ZDLL)]} {
        sevenzip initialize $env(7ZDLL)
    } else {
        sevenzip initialize            
    }
}

if {[info proc readFile] eq ""} {
    proc readFile {filename} {
        set f [open $filename "r"]
        try {
            fconfigure $f -encoding utf-8
            return [read $f]
        } finally {
            close $f
        }
    }
}

if {[info proc writeFile] eq ""} {
    proc writeFile {filename data} {
        set f [open $filename "w"]
        try {
            fconfigure $f -encoding utf-8
            puts -nonewline $f $data
        } finally {
            close $f
        }
    }
}

if {[testConstraint vfsdebug]} {
    puts stderr vfs::debug
    proc vfserror {} {vfs::log $::errorInfo}
    vfs::filesystem internalerror vfserror
    set vfs::debug 1
}

testConstraint have7zip [sevenzip isinitialized]
testConstraint encodingOk [expr {"тест" eq "\u0442\u0435\u0441\u0442"}]

set invalidTimeBugFile [file join [testsDirectory] files Microsoft.VisualStudio.Setup.TestMsi.msi] 
testConstraint invalidTimeBug [file exists $invalidTimeBugFile]

test sevenzipvfs-1.0 {mount params} -body {
    vfs::sevenzip::Mount
} -returnCodes 1 -result {wrong # args: should be "vfs::sevenzip::Mount zipfile local ?arg ...?"}

test sevenzipvfs-1.1 {mount non-existent file} -constraints {have7zip} -cleanup {
} -body {
    vfs::sevenzip::Mount [file join [testsDirectory] files notexistent] mnt
} -returnCodes 1 -result "couldn't open file \"[file join [testsDirectory] files notexistent]\": no such file or directory"

test sevenzipvfs-1.2 {mount unknown file} -constraints {have7zip} -cleanup {
} -body {
    vfs::sevenzip::Mount [file join [testsDirectory] files test.txt] mnt
} -returnCodes 1 -result {not supported}

test sevenzipvfs-1.3 {mount/unmount simple file} -constraints {have7zip} -cleanup {
    unset -nocomplain mnt
} -body {
    set mnt [vfs::sevenzip::Mount [file join [testsDirectory] files test.7z] mnt]
    vfs::sevenzip::Unmount $mnt mnt
} -result {}

test sevenzipvfs-1.3.0 {check archive cache is clean after unmount} -constraints {have7zip} -setup {
    set mnt [vfs::sevenzip::Mount [file join [testsDirectory] files test.7z] mnt]
} -cleanup {
    unset mnt
} -body {
    vfs::sevenzip::Unmount $mnt mnt
    dict exists $vfs::sevenzip::Cache $mnt
} -result 0

test sevenzipvfs-1.3.1 {check simple archive cache} -constraints {have7zip} -setup {
    set mnt [vfs::sevenzip::Mount [file join [testsDirectory] files test.7z] mnt]
} -cleanup {
    vfs::sevenzip::Unmount $mnt mnt; unset mnt
} -body {
    dict get $vfs::sevenzip::Cache $mnt
} -result {. test.txt}

test sevenzipvfs-1.3.2 {check special archive cache} -constraints {have7zip} -setup {
    set mnt [vfs::sevenzip::Mount [file join [testsDirectory] files testDIRS.tgz] mnt]
} -cleanup {
    vfs::sevenzip::Unmount $mnt mnt; unset mnt
} -body {
    dict get $vfs::sevenzip::Cache $mnt
} -result {. {{[Content]}}}

test sevenzipvfs-1.3.3 {check complex archive cache} -constraints {have7zip} -setup {
    set mnt [vfs::sevenzip::Mount [file join [testsDirectory] files testDIRS.7z] mnt]
} -cleanup {
    vfs::sevenzip::Unmount $mnt mnt; unset mnt
} -body {
    dict keys [dict get $vfs::sevenzip::Cache $mnt]
} -result {. testDIRS}

test sevenzipvfs-1.3.4 {check complex archive w/o dirs cache} -constraints {have7zip} -setup {
    set mnt [vfs::sevenzip::Mount [file join [testsDirectory] files testDIRS.arj] mnt]
} -cleanup {
    vfs::sevenzip::Unmount $mnt mnt; unset mnt
} -body {
    dict keys [dict get $vfs::sevenzip::Cache $mnt]
} -result {. testDIRS}

foreach x {7z zip rar arj tar} {
    foreach {n i r} {
        1 {} {directory 1 0}
        2 testDIRS {directory 1 0}
        3 testDIRX {unknown 0 0}
        4 testDIRS/test4.txt {file 0 1}
        5 testDIRS/testX.txt {unknown 0 0}  
    } {
        test sevenzipvfs-1.3.5-$x.$n {check item exists in cache} -constraints {have7zip} -setup {
            set mnt [vfs::sevenzip::Mount [file join [testsDirectory] files testDIRS.$x] mnt]
            set type "unknown"
        } -cleanup {
            vfs::sevenzip::Unmount $mnt mnt; unset mnt
            unset type
        } -body {
            catch {set type [file type [file join mnt $i]]}
            list $type \
                [vfs::sevenzip::CacheDirExists $mnt $i] \
                [vfs::sevenzip::CacheFileExists $mnt $i]
        } -result $r
        unset n i r
    } 
}

foreach x {7z zip rar arj tar} {
    test sevenzipvfs-1.3.6-$x {check files from cache} -constraints {have7zip} -setup {
        set mnt [vfs::sevenzip::Mount [file join [testsDirectory] files testDIRS.$x] mnt]
    } -cleanup {
        vfs::sevenzip::Unmount $mnt mnt; unset mnt
    } -body {
        list \
                [lsort [vfs::sevenzip::CacheGetDir $mnt {}]] \
                [lsort [vfs::sevenzip::CacheGetDir $mnt testDIRS/test2]] \
                [lsort [vfs::sevenzip::CacheGetDir $mnt testDIRS/test3/test32]]
    } -result {testDIRS {test21.txt test22.txt test23.txt} test321.txt}
    unset x
}

test sevenzipvfs-1.4.1 {mount/glob simple file} -constraints {have7zip} -setup {
    set mnt [vfs::sevenzip::Mount [file join [testsDirectory] files test.7z] mnt]
} -cleanup {
    vfs::sevenzip::Unmount $mnt mnt; unset mnt
} -body {
    glob mnt/*
} -result {mnt/test.txt}

test sevenzipvfs-1.4.2 {mount/glob simple file tail} -constraints {have7zip} -setup {
    set mnt [vfs::sevenzip::Mount [file join [testsDirectory] files test.7z] mnt]
} -cleanup {
    vfs::sevenzip::Unmount $mnt mnt; unset mnt
} -body {
    glob -directory mnt -tails *
} -result {test.txt}

test sevenzipvfs-1.5.1 {mount/open simple file} -constraints {have7zip} -setup {
    set mnt [vfs::sevenzip::Mount [file join [testsDirectory] files test.7z] mnt]
} -cleanup {
    vfs::sevenzip::Unmount $mnt mnt; unset mnt
} -body {
    readFile mnt/test.txt
} -result {test}

test sevenzipvfs-1.5.2 {mount/open complex file} -constraints {have7zip} -setup {
    set mnt [vfs::sevenzip::Mount [file join [testsDirectory] files testDIRS.7z] mnt]
} -cleanup {
    vfs::sevenzip::Unmount $mnt mnt; unset mnt
} -body {
    readFile mnt/testDIRS/test4.txt
} -result {test4}

test sevenzipvfs-1.5.3 {mount/open complex zip file} -constraints {have7zip} -setup {
    set mnt [vfs::sevenzip::Mount [file join [testsDirectory] files testDIRS.zip] mnt]
} -cleanup {
    vfs::sevenzip::Unmount $mnt mnt; unset mnt
} -body {
    readFile mnt/testDIRS/test4.txt
} -result {test4}

foreach {n a g r} {
    1 "" "mnt"              "mnt"
    2 "" "mnt/"             "mnt"
    3 "" "mnt/../mnt"       "mnt/../mnt"
    4 "" "mnt/testDIRS"     "mnt/testDIRS"
    5 "" "mnt/*"            "mnt/testDIRS"
    6 "-types d" "mnt/*"    "mnt/testDIRS"
    7 "-types f" "mnt/*"    ""
    8 "" "mnt/testDIRS/test4.txt"       "mnt/testDIRS/test4.txt"
    9 "-dir mnt" ""                     "mnt"
    A "-dir mnt/../mnt" ""              "mnt/../mnt"
    C "-dir mnt/testDIRS" ""            "mnt/testDIRS"
    D "-dir mnt/testDIRS/test4.txt" ""  "mnt/testDIRS/test4.txt"
} {
    test sevenzipvfs-1.6.$n "glob $a '$g'" -constraints {have7zip} -setup {
        set mnt [vfs::sevenzip::Mount [file join [testsDirectory] files testDIRS.zip] mnt]
    } -cleanup {
        vfs::sevenzip::Unmount $mnt mnt; unset mnt
    } -body {
        glob -nocomplain {*}$a $g
    } -result $r

    test sevenzipvfs-1.6zip.$n "zip glob $a $g" -constraints {vfsdebug} -setup {
        package require vfs::zip
        set mnt [vfs::zip::Mount [file join [testsDirectory] files testDIRS.zip] mnt]
    } -cleanup {
        vfs::zip::Unmount $mnt mnt; unset mnt
    } -body {
        glob -nocomplain {*}$a $g
    } -result $r

    unset n g r
}

test sevenzipvfs-2.1.invalidTimeBug {invalidTimeBug} -constraints {have7zip invalidTimeBug} -setup {
    set mnt [vfs::sevenzip::Mount $invalidTimeBugFile mnt]
} -cleanup {
    vfs::sevenzip::Unmount $mnt mnt; unset mnt
} -body {
    file isfile mnt/!_Columns
} -result {1}


cleanupTests
return

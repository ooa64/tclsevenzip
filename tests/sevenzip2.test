if {[lsearch [namespace children] ::tcltest] < 0} {
    package require tcltest 2
    namespace import ::tcltest::*
}


package require sevenzip 1
catch {
    if {[info exist env(7ZDLL)]} {
        sevenzip initialize $env(7ZDLL)
    } else {
        sevenzip initialize            
    }
}

if {[info proc readFile] eq ""} {
    proc readFile {filename} {
        set f [open $filename "r"]
        try {
            fconfigure $f -encoding utf-8
            return [read $f]
        } finally {
            close $f
        }
    }
}

if {[info proc writeFile] eq ""} {
    proc writeFile {filename data} {
        set f [open $filename "w"]
        try {
            fconfigure $f -encoding utf-8
            puts -nonewline $f $data
        } finally {
            close $f
        }
    }
}

customMatch nocase {string equal -nocase}
testConstraint have7zip [sevenzip isinitialized]
testConstraint haveMemchan [expr {![catch {package require tcl::chan::memchan}]}]
testConstraint encodingOk [expr {"тест" eq "\u0442\u0435\u0441\u0442"}]

if {[testConstraint have7zip]} {
    foreach e {7z zip rar tar arj} {
        testConstraint supported.$e [expr {$e in [sevenzip extensions]}]
    }
}


test sevenzip2-1.0 {create syntax} -body {
    sevenzip create
} -returnCodes 1 -result {wrong # args: should be "sevenzip create ?options? path list"}

test sevenzip2-1.1 {create syntax} -body {
    sevenzip create xxx
} -returnCodes 1 -result {wrong # args: should be "sevenzip create ?options? path list"}

test sevenzip2-1.2 {create syntax}  -body {
    sevenzip create xxx xxx {}
} -returnCodes 1 -result {bad option "xxx": must be -properties, -forcetype, -password, or -channel}

test sevenzip2-1.3 {create syntax}  -body {
    sevenzip create -properties xxx {}
} -returnCodes 1 -result {"-properties" option must be followed by property dictionary}

test sevenzip2-1.4 {create syntax}  -body {
    sevenzip create -properties {xxx} xxx {}
} -returnCodes 1 -result {"-properties" option must be followed by an even-length list}

test sevenzip2-1.5 {create syntax}  -body {
    sevenzip create -forcetype xxx {}
} -returnCodes 1 -result {"-forcetype" option must be followed by type}

test sevenzip2-1.6 {create syntax} -body {
    sevenzip create -password xxx {}
} -returnCodes 1 -result {"-password" option must be followed by password}

test sevenzip2-2.0 {create syntax} -body {
    sevenzip create -channel xxx {}
} -returnCodes 1 -result {can not find channel named "xxx"}

test sevenzip2-2.2 {create archive of invalid type} -constraints {have7zip} -body {
    sevenzip create -forcetype xxx xxx {}
} -returnCodes 1 -result {not supported}

test sevenzip2-2.3 {create empty default archive (7z)} -constraints {have7zip haveMemchan} -setup {
    set c [::tcl::chan::memchan]
} -cleanup {
    close $c; unset c
} -body {
    sevenzip create -channel $c {}
    expr {[tell $c] > 0}
} -result 1

test sevenzip2-2.4 {create simple default archive (7z)} -constraints {have7zip haveMemchan} -setup {
    set c [::tcl::chan::memchan]
} -cleanup {
    close $c; unset c
} -body {
    sevenzip create -channel $c [list [file join [testsDirectory] files test.txt]]
    expr {[tell $c] > 0}
} -result 1

set {sevenzip2-2.5.#} 0
test sevenzip2-2.5.# {create simple archives of different types} {have7zip haveMemchan} {
    set {sevenzip2-2.5.#} 1
} 1

if {[set {sevenzip2-2.5.#}]} {
    set l [list [file join [testsDirectory] files test.txt]]
    foreach ext [lmap e [sevenzip extensions] {if {[sevenzip updatable $e]} {set e} else continue}] {
        test sevenzip2-2.5.$ext {create simple $ext archive} -setup {
            set c [::tcl::chan::memchan]
        } -cleanup {
            close $c; unset c
        } -body {
            sevenzip create -forcetype $ext -channel $c $l
            expr {[tell $c] > 0}
        } -result 1
        unset ext
    }
    unset l
}

test sevenzip2-2.6 {create/list simple archive (7z)} -constraints {have7zip haveMemchan} -setup {
    set l [list [file join [testsDirectory] files test.txt]]
    set c [::tcl::chan::memchan]
    set z ""
} -cleanup {
    catch {rename $z ""}; unset z
    close $c; unset c
    unset l
} -body {
    sevenzip create -forcetype 7z -channel $c $l
    seek $c 0 start
    set z [sevenzip open -forcetype 7z -channel $c]
    $z list
} -result [list [file join [testsDirectory] files test.txt]]

test sevenzip2-2.7 {create/extract simple archive (7z)} -constraints {have7zip haveMemchan} -setup {
    set l [list [file join [testsDirectory] files test.txt]]
    set c [::tcl::chan::memchan]
    set o [::tcl::chan::memchan]
    set z ""
} -cleanup {
    catch {rename $z ""}; unset z
    close $c; unset c
    close $o; unset o
    unset l
} -body {
    sevenzip create -forcetype 7z -channel $c $l
    seek $c 0 start
    set z [sevenzip open -forcetype 7z -channel $c]
    $z extract -channel $o [lindex $l 0]
    seek $o 0 start
    read $o
} -result {test}

test sevenzip2-2.8.1 {create/extract encrypted archive (7z)} -constraints {have7zip haveMemchan} -setup {
    set l [list [file join [testsDirectory] files test.txt]]
    set c [::tcl::chan::memchan]
    set o [::tcl::chan::memchan]
    set z ""
} -cleanup {
    catch {rename $z ""}; unset z
    close $c; unset c
    close $o; unset o
    unset l
} -body {
    sevenzip create -forcetype 7z -password TEST -channel $c $l
    seek $c 0 start
    set z [sevenzip open -forcetype 7z -channel $c]
    $z extract -channel $o [lindex $l 0]
} -result {need password} -returnCodes 1

test sevenzip2-2.8.2 {create/extract encrypted archive (7z)} -constraints {have7zip haveMemchan} -setup {
    set l [list [file join [testsDirectory] files test.txt]]
    set c [::tcl::chan::memchan]
    set o [::tcl::chan::memchan]
    set z ""
} -cleanup {
    catch {rename $z ""}; unset z
    close $c; unset c
    close $o; unset o
    unset l
} -body {
    # sevenzip create -forcetype 7z -channel $c $l
    sevenzip create -forcetype 7z -password TEST -channel $c $l
    flush $c
    seek $c 0 start
    # set z [sevenzip open -forcetype 7z -channel $c]
    set z [sevenzip open -forcetype 7z -password TEST -channel $c]
    $z extract -channel $o [lindex $l 0]
    seek $o 0 start
    read $o
} -result {test}

test sevenzip2-2.8.3 {create/extract encrypted archive (7z)} -constraints {have7zip haveMemchan} -setup {
    set l [list [file join [testsDirectory] files test.txt]]
    set c [::tcl::chan::memchan]
    set o [::tcl::chan::memchan]
    set z ""
} -cleanup {
    catch {rename $z ""}; unset z
    close $c; unset c
    close $o; unset o
    unset l
} -body {
    sevenzip create -forcetype 7z -password TEST -channel $c $l
    seek $c 0 start
    set z [sevenzip open -forcetype 7z -channel $c]
    $z extract -password TEST -channel $o [lindex $l 0]
    seek $o 0 start
    read $o
} -result {test}

test sevenzip2-3.4 {create simple file archive (7z)} -constraints {have7zip} -setup {
    set f [file join [temporaryDirectory] sevenzip2-3.4.7z]
} -cleanup {
    file delete -force $f; unset f
} -body {
    sevenzip create -forcetype 7z $f [list [file join [testsDirectory] files test.txt]]
    expr {[file size $f] > 0}
} -result 1

test sevenzip2-3.7 {create/extract simple archive (7z)} -constraints {have7zip haveMemchan} -setup {
    set l [list [file join [testsDirectory] files test.txt]]
    set f [file join [temporaryDirectory] sevenzip2-3.7.7z]
    set o [file join [temporaryDirectory] sevenzip2-3.7.txt]
    set z ""
} -cleanup {
    catch {rename $z ""}; unset z
    file delete -force $f; unset f
    file delete -force $o; unset o
    unset l
} -body {
    sevenzip create -forcetype 7z $f $l
    set z [sevenzip open -forcetype 7z $f]
    $z extract $o [lindex $l 0]
    readFile $o    
} -result {test}

test sevenzip2-3.8 {create/extract encrypted archive (7z)} -constraints {have7zip} -setup {
    set l [list [file join [testsDirectory] files test.txt]]
    set f [file join [temporaryDirectory] sevenzip2-3.7.7z]
    set o [file join [temporaryDirectory] sevenzip2-3.7.txt]
    set z ""
} -cleanup {
    catch {rename $z ""}; unset z
    file delete -force $f; unset f
    file delete -force $o; unset o
    unset l
} -body {
    sevenzip create -forcetype 7z -password TEST $f $l
    set z [sevenzip open -forcetype 7z -password TEST $f]
    $z extract $o [lindex $l 0]
    readFile $o
} -result {test}

test sevenzip2-3.8.1 {create/extract encrypted item (7z)} -constraints {have7zip} -setup {
    set l [list [file join [testsDirectory] files test.txt]]
    set f [file join [temporaryDirectory] sevenzip2-3.7.7z]
    set o [file join [temporaryDirectory] sevenzip2-3.7.txt]
    set z ""
} -cleanup {
    catch {rename $z ""}; unset z
    file delete -force $f; unset f
    file delete -force $o; unset o
    unset l
} -body {
    sevenzip create -forcetype 7z -password TEST $f $l
    set z [sevenzip open -forcetype 7z $f]
    $z extract -password TEST $o [lindex $l 0]
    readFile $o
} -result {test}

test sevenzip2-4.0 {create complex archive (7z)} -constraints {have7zip haveMemchan} -setup {
    set c [::tcl::chan::memchan]
    set d "sevenzip2-4.0"
    set z ""
    set l {}
    set w [pwd]
    cd [temporaryDirectory]
    file mkdir $d; lappend l $d
    file mkdir [file join $d subdir1]; lappend l [file join $d subdir1]
    file mkdir [file join $d subdir2]; lappend l [file join $d subdir2]
    writeFile [file join $d file1.txt] "This is file 1."; lappend l [file join $d file1.txt]
    writeFile [file join $d subdir1 file2.txt] "This is file 2."; lappend l [file join $d subdir1 file2.txt]
    writeFile [file join $d subdir2 file3.txt] "This is file 3."; lappend l [file join $d subdir2 file3.txt]
} -cleanup {
    file delete -force $d; unset d
    catch {rename $z ""}; unset z
    close $c; unset c
    cd $w; unset w
    unset l
} -body {
    sevenzip create -forcetype 7z -channel $c $l
    seek $c 0 start
    set z [sevenzip open -forcetype 7z -channel $c]
    join [$z list] \n
} -result {sevenzip2-4.0
sevenzip2-4.0/subdir1
sevenzip2-4.0/subdir2
sevenzip2-4.0/file1.txt
sevenzip2-4.0/subdir1/file2.txt
sevenzip2-4.0/subdir2/file3.txt}

cleanupTests
return

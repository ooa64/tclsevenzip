source [file join [file dirname [info script]] allutils.tcl]

package require sevenzip 1
if {![sevenzip isinitialized]} {
    catch {
        if {[info exist env(7ZDLL)]} {
            sevenzip initialize $env(7ZDLL)
        } else {
            sevenzip initialize
        }
    }
}

customMatch nocase {string equal -nocase}
testConstraint have7zip [sevenzip isinitialized]
testConstraint haveMemchan [expr {![catch {package require tcl::chan::memchan}]}]

set updatableExtensions {}
if {[testConstraint have7zip]} {
    foreach e [sevenzip extensions] {
        if {[sevenzip updatable $e]} {
            lappend updatableExtensions $e
        }
    }
    unset e
}


test sevenzip2-1.0 {create syntax} -body {
    sevenzip create
} -returnCodes 1 -result {wrong # args: should be "sevenzip create ?options? path list"}

test sevenzip2-1.1 {create syntax} -body {
    sevenzip create xxx
} -returnCodes 1 -result {wrong # args: should be "sevenzip create ?options? path list"}

test sevenzip2-1.2 {create syntax}  -body {
    sevenzip create xxx xxx {}
} -returnCodes 1 -result {bad option "xxx": must be -properties, -forcetype, -password, -inputchannel, or -channel}

test sevenzip2-1.3 {create syntax}  -body {
    sevenzip create -properties xxx {}
} -returnCodes 1 -result {"-properties" option must be followed by property dictionary}

test sevenzip2-1.4 {create syntax}  -body {
    sevenzip create -properties {xxx} xxx {}
} -returnCodes 1 -result {"-properties" option must be followed by an even-length list}

test sevenzip2-1.5 {create syntax}  -body {
    sevenzip create -forcetype xxx {}
} -returnCodes 1 -result {"-forcetype" option must be followed by type}

test sevenzip2-1.6 {create syntax} -body {
    sevenzip create -password xxx {}
} -returnCodes 1 -result {"-password" option must be followed by password}

test sevenzip2-2.0 {create from file not found} -constraints have7zip -setup {
    set f [file join [temporaryDirectory] sevenzip2.7z]
} -cleanup {
    catch {file delete -force $f}; unset f
} -body {
    sevenzip create -forcetype 7z $f [list [file join [testsDirectory] files notexistent]]
    expr {[file size $f] > 0}
} -returnCodes 1 -result {couldn't open "*/tests/files/notexistent": no such file or directory} -match glob

test sevenzip2-2.1.0 {create to invalid channel} -constraints have7zip -body {
    sevenzip create -channel xxx {}
} -returnCodes 1 -result {can not find channel named "xxx"}

test sevenzip2-2.1.1 {create from invalid channel} -constraints have7zip -body {
    sevenzip create -inputchannel iii xxx {}
} -returnCodes 1 -result {can not find channel named "iii"}

test sevenzip2-2.2 {create archive of invalid type} -constraints have7zip -body {
    sevenzip create -forcetype xxx xxx {}
} -returnCodes 1 -match nocase -result {Not supported}

test sevenzip2-2.3 {create empty default archive (7z)} -constraints have7zip -setup {
    set f [file join [temporaryDirectory] sevenzip2.7z]
} -cleanup {
    catch {file delete -force $f}; unset f
} -body {
    sevenzip create $f {}
    expr {[file size $f] > 0}
} -result 1

test sevenzip2-2.3m {create empty default archive (7z)} -constraints {have7zip haveMemchan} -setup {
    set c [::tcl::chan::memchan]
} -cleanup {
    close $c; unset c
} -body {
    sevenzip create -channel $c {}
    expr {[tell $c] > 0}
} -result 1

test sevenzip2-2.4 {create simple file archive (7z)} -constraints have7zip -setup {
    set f [file join [temporaryDirectory] sevenzip2.7z]
} -cleanup {
    catch {file delete -force $f}; unset f
} -body {
    sevenzip create -forcetype 7z $f [list [file join [testsDirectory] files test.txt]]
    expr {[file size $f] > 0}
} -result 1

test sevenzip2-2.4m {create simple default archive (7z)} -constraints {have7zip haveMemchan} -setup {
    set c [::tcl::chan::memchan]
} -cleanup {
    close $c; unset c
} -body {
    sevenzip create -channel $c [list [file join [testsDirectory] files test.txt]]
    expr {[tell $c] > 0}
} -result 1

test sevenzip2-2.4.1 {create simple twofile archive (7z)} -constraints have7zip -setup {
    set f [file join [temporaryDirectory] sevenzip2.7z]
} -cleanup {
    catch {file delete -force $f}; unset f
} -body {
    sevenzip create -forcetype 7z $f [list [file join [testsDirectory] files test.txt] \
            [file join [testsDirectory] sevenzip2.test]]
    expr {[file size $f] > 0}
} -result 1

foreach e $updatableExtensions {
    set l [list [file join [testsDirectory] files test.txt]]
    set l2 [list [file join [testsDirectory] files test.txt] [file join [testsDirectory] sevenzip2.test]]

    test sevenzip2-2.5.$e {create simple $e archive} -constraints have7zip -setup {
        set f [file join [temporaryDirectory] sevenzip2.7z]
    } -cleanup {
        catch {file delete -force $f}; unset f
    } -body {
        sevenzip create -forcetype $e $f $l
        expr {[file size $f] > 0}
    } -result 1

    test sevenzip2-2.5m.$e {create simple $e archive} -constraints {have7zip haveMemchan} -setup {
        set c [::tcl::chan::memchan]
    } -cleanup {
        close $c; unset c
    } -body {
        sevenzip create -forcetype $e -channel $c $l
        expr {[tell $c] > 0}
    } -result 1

    if {$e in {7z tar wim zip}} {
        test sevenzip2-2.5.$e.1 {create simple twofile $e archive} -constraints have7zip -setup {
            set f [file join [temporaryDirectory] sevenzip2.$e]
        } -cleanup {
            catch {file delete -force $f}; unset f
        } -body {
            sevenzip create -forcetype $e $f $l2
            expr {[file size $f] > 0}
        } -result 1
    }

    unset e l l2
}

test sevenzip2-2.6 {create/list simple archive (7z)} -constraints have7zip -setup {
    set l [list [file join [testsDirectory] files test.txt]]
    set f [file join [temporaryDirectory] sevenzip2.7z]
    set z ""
} -cleanup {
    catch {rename $z ""}; unset -nocomplain z
    catch {file delete -force $f}; unset f
    unset l
} -body {
    sevenzip create -forcetype 7z $f $l
    set z [sevenzip open -forcetype 7z $f]
    $z list
} -result [list [file join [testsDirectory] files test.txt]]

test sevenzip2-2.6m {create/list simple archive (7z)} -constraints {have7zip haveMemchan} -setup {
    set l [list [file join [testsDirectory] files test.txt]]
    set c [::tcl::chan::memchan]
    set z ""
} -cleanup {
    catch {rename $z ""}; unset z
    close $c; unset c
    unset l
} -body {
    sevenzip create -forcetype 7z -channel $c $l
    seek $c 0 start
    set z [sevenzip open -forcetype 7z -channel $c]
    $z list
} -result [list [file join [testsDirectory] files test.txt]]

test sevenzip2-2.6.1 {create/list archive from channel (7z)} -constraints have7zip -setup {
    set l [list [file join [testsDirectory] files test.txt]]
    set f [file join [temporaryDirectory] sevenzip2.7z]
    set i [open [lindex $l 0]]
    set z ""
} -cleanup {
    catch {rename $z ""}; unset z
    catch {file delete -force $f}; unset f
    close $i; unset i
    unset l
} -body {
    sevenzip create -forcetype 7z -inputchannel $i $f $l
    set z [sevenzip open -forcetype 7z $f]
    $z list
} -result [list [file join [testsDirectory] files test.txt]]

test sevenzip2-2.7 {create/extract simple archive (7z)} -constraints have7zip -setup {
    set l [list [file join [testsDirectory] files test.txt]]
    set f [file join [temporaryDirectory] sevenzip2.7z]
    set e [file join [temporaryDirectory] sevenzip2.txt]
    set z ""
} -cleanup {
    catch {rename $z ""}; unset z
    catch {file delete -force $e}; unset e
    catch {file delete -force $f}; unset f
    unset l
} -body {
    sevenzip create -forcetype 7z $f $l
    set z [sevenzip open -forcetype 7z $f]
    $z extract $e [lindex $l 0]
    readFile $e
} -result {test}

test sevenzip2-2.7m {create/extract simple archive (7z)} -constraints {have7zip haveMemchan} -setup {
    set l [list [file join [testsDirectory] files test.txt]]
    set c [::tcl::chan::memchan]
    set o [::tcl::chan::memchan]
    set z ""
} -cleanup {
    catch {rename $z ""}; unset z
    close $c; unset c
    close $o; unset o
    unset l
} -body {
    sevenzip create -forcetype 7z -channel $c $l
    seek $c 0 start
    set z [sevenzip open -forcetype 7z -channel $c]
    $z extract -channel $o [lindex $l 0]
    seek $o 0 start
    read $o
} -result {test}

test sevenzip2-2.7.1 {create/extract archive from channel (7z)} -constraints have7zip -setup {
    set l [list [file join [testsDirectory] files test.txt]]
    set f [file join [temporaryDirectory] sevenzip2.7z]
    set e [file join [temporaryDirectory] sevenzip2.txt]
    set i [open [lindex $l 0]]
    set z ""
} -cleanup {
    catch {rename $z ""}; unset z
    catch {file delete -force $e}; unset e
    catch {file delete -force $f}; unset f
    close $i; unset i
    unset l
} -body {
    sevenzip create -forcetype 7z -inputchannel $i $f $l
    set z [sevenzip open -forcetype 7z $f]
    $z extract $e [lindex $l 0]
    readFile $e
} -result {test}

test sevenzip2-2.8 {create/extract encrypted archive (7z)} -constraints have7zip -setup {
    set l [list [file join [testsDirectory] files test.txt]]
    set f [file join [temporaryDirectory] sevenzip2.7z]
    set o [file join [temporaryDirectory] sevenzip2.txt]
    set z ""
} -cleanup {
    catch {rename $z ""}; unset z
    catch {file delete -force $f}; unset f
    catch {file delete -force $o}; unset o
    unset l
} -body {
    sevenzip create -forcetype 7z -password TEST $f $l
    set z [sevenzip open -forcetype 7z -password TEST $f]
    $z extract $o [lindex $l 0]
    readFile $o
} -result {test}

test sevenzip2-2.8m {create/extract encrypted archive (7z)} -constraints {have7zip haveMemchan frozenMemchanBug} -setup {
    set l [list [file join [testsDirectory] files test.txt]]
    set c [::tcl::chan::memchan]
    set o [::tcl::chan::memchan]
#     fconfigure $c -translation binary -encoding binary -buffering none
#     fconfigure $o -translation binary -encoding binary -buffering none
#     puts MEMCHAN_C:[fconfigure $c]
#     puts MEMCHAN_O:[fconfigure $o]
    set z ""
} -cleanup {
    catch {rename $z ""}; unset z
    close $c; unset c
    close $o; unset o
    unset l
} -body {
    sevenzip create -forcetype 7z -password TEST -channel $c $l
    seek $c 0 start
    set z [sevenzip open -forcetype 7z -password TEST -channel $c]
    $z extract -channel $o [lindex $l 0]
    seek $o 0 start
    read $o
} -result {test}

test sevenzip2-2.8.1 {create/extract encrypted item no password (7z)} -constraints have7zip -setup {
    set l [list [file join [testsDirectory] files test.txt]]
    set f [file join [temporaryDirectory] sevenzip2.7z]
    set o [file join [temporaryDirectory] sevenzip2.txt]
    set z ""
} -cleanup {
    catch {rename $z ""}; unset z
    catch {file delete -force $f}; unset f
    catch {file delete -force $o}; unset o
    unset l
} -body {
    sevenzip create -forcetype 7z -password TEST $f $l
    set z [sevenzip open -forcetype 7z $f]
    $z extract $o [lindex $l 0]
} -result {Need password} -match nocase -returnCodes 1

test sevenzip2-2.8.1m {create/extract encrypted archive no password (7z)} -constraints {have7zip haveMemchan} -setup {
    set l [list [file join [testsDirectory] files test.txt]]
    set c [::tcl::chan::memchan]
    set o [::tcl::chan::memchan]
    set z ""
} -cleanup {
    catch {rename $z ""}; unset z
    close $c; unset c
    close $o; unset o
    unset l
} -body {
    sevenzip create -forcetype 7z -password TEST -channel $c $l
    seek $c 0 start
    set z [sevenzip open -forcetype 7z -channel $c]
    $z extract -channel $o [lindex $l 0]
} -result {Need password} -match nocase -returnCodes 1

test sevenzip2-2.8.2 {create/extract encrypted item invalid password (7z)} -constraints have7zip -setup {
    set l [list [file join [testsDirectory] files test.txt]]
    set f [file join [temporaryDirectory] sevenzip2.7z]
    set o [file join [temporaryDirectory] sevenzip2.txt]
    set z ""
} -cleanup {
    catch {rename $z ""}; unset z
    catch {file delete -force $f}; unset f
    catch {file delete -force $o}; unset o
    unset l
} -body {
    sevenzip create -forcetype 7z -password TEST $f $l
    set z [sevenzip open -forcetype 7z -password TESTX $f]
    $z extract $o [lindex $l 0]
} -result {*Unspecified error} -match glob -returnCodes 1

test sevenzip2-2.8.2m {create/extract encrypted archive no password (7z)} -constraints {have7zip haveMemchan frozenMemchanBug} -setup {
    set l [list [file join [testsDirectory] files test.txt]]
    set c [::tcl::chan::memchan]
    set o [::tcl::chan::memchan]
    set z ""
} -cleanup {
    catch {rename $z ""}; unset z
    close $c; unset c
    close $o; unset o
    unset l
} -body {
    sevenzip create -forcetype 7z -password TEST -channel $c $l
    seek $c 0 start
    set z [sevenzip open -forcetype 7z -password TESTX -channel $c]
    $z extract -channel $o [lindex $l 0]
} -result {*Unspecified error} -match glob -returnCodes 1

test sevenzip2-2.9 {create complex archive (7z)} -constraints have7zip -setup {
    set f [file join [temporaryDirectory] sevenzip2.7z]
    set d "sevenzip2"
    set z ""
    set l {}
    set w [pwd]
    cd [temporaryDirectory]
    file mkdir $d; lappend l $d
    file mkdir [file join $d subdir1]; lappend l [file join $d subdir1]
    file mkdir [file join $d subdir2]; lappend l [file join $d subdir2]
    writeFile [file join $d file1.txt] "This is file 1."; lappend l [file join $d file1.txt]
    writeFile [file join $d subdir1 file2.txt] "This is file 2."; lappend l [file join $d subdir1 file2.txt]
    writeFile [file join $d subdir2 file3.txt] "This is file 3."; lappend l [file join $d subdir2 file3.txt]
} -cleanup {
    catch {rename $z ""}; unset z
    catch {file delete -force $d}; unset d
    catch {file delete -force $f}; unset f
    cd $w; unset w
    unset l
} -body {
    sevenzip create -forcetype 7z $f $l
    set z [sevenzip open -forcetype 7z $f]
    join [$z list] \n
} -result {sevenzip2
sevenzip2/subdir1
sevenzip2/subdir2
sevenzip2/file1.txt
sevenzip2/subdir1/file2.txt
sevenzip2/subdir2/file3.txt}

test sevenzip2-2.9m {create complex archive (7z)} -constraints {have7zip haveMemchan} -setup {
    set c [::tcl::chan::memchan]
    set d "sevenzip2"
    set z ""
    set l {}
    set w [pwd]
    cd [temporaryDirectory]
    file mkdir $d; lappend l $d
    file mkdir [file join $d subdir1]; lappend l [file join $d subdir1]
    file mkdir [file join $d subdir2]; lappend l [file join $d subdir2]
    writeFile [file join $d file1.txt] "This is file 1."; lappend l [file join $d file1.txt]
    writeFile [file join $d subdir1 file2.txt] "This is file 2."; lappend l [file join $d subdir1 file2.txt]
    writeFile [file join $d subdir2 file3.txt] "This is file 3."; lappend l [file join $d subdir2 file3.txt]
} -cleanup {
    catch {file delete -force $d}; unset d
    catch {rename $z ""}; unset z
    close $c; unset c
    cd $w; unset w
    unset l
} -body {
    sevenzip create -forcetype 7z -channel $c $l
    seek $c 0 start
    set z [sevenzip open -forcetype 7z -channel $c]
    join [$z list] \n
} -result {sevenzip2
sevenzip2/subdir1
sevenzip2/subdir2
sevenzip2/file1.txt
sevenzip2/subdir1/file2.txt
sevenzip2/subdir2/file3.txt}

test sevenzip2-3.0 {check attributes for creating archive (7z)} -constraints {have7zip win} -setup {
    set z ""
    set f "sevenzip2.7z"
    set e "sevenzip2.txt"
    set w [pwd]
    cd [temporaryDirectory]
    writeFile $e $e
    file attributes $e -readonly 1 -hidden 1 -system 1 -archive 1
    file mtime $e [clock scan "2100-01-02 03:04:05"]
} -cleanup {
    catch {rename $z ""}; unset z
    catch {file delete -force $f}; unset f
    catch {file delete -force $e}; unset e
    cd $w; unset w
    unset -nocomplain a
} -body {
    sevenzip create -forcetype 7z $f [list $e]
    set z [sevenzip open -forcetype 7z $f]
    array set a [lindex [$z list -info] 0]
    list $a(size) $a(mtime) $a(attrib)
} -result {13 4102535045 39}

foreach {n p r} {
    0 {m lzma} LZMA:*
    1 {m copy} COPY
    2 {x 9} LZMA2:*
    3 {x 0} COPY
    4 {m lzma x 9} LZMA:*
    5 {m lzma x 0} LZMA:*
    6 {m copy x 9} COPY
    7 {m copy x 0} COPY
    8 {x 9 m lzma} LZMA:*
    9 {x 0 m lzma} LZMA:*
    A {x 9 m copy} COPY
    B {x 0 m copy} COPY
} { 
    test sevenzip2-4.0.$n "create simple 7z archive with property {$p}" -constraints have7zip -setup {
        set l [list [file join [testsDirectory] files test.txt]]
        set f [file join [temporaryDirectory] sevenzip2.7z]
        set z ""
    } -cleanup {
        catch {rename $z ""}; unset -nocomplain z
        catch {file delete -force $f}; unset f
        unset l
    } -body {
        sevenzip create -properties $p -forcetype 7z $f $l
        set z [sevenzip open -forcetype 7z $f]
        string toupper [dict get [lindex [$z list -info] 0] method]
    } -result $r -match glob
    unset n p r
}

foreach {n p v r} {
    0 s on 1
    1 s off 0
} { 
    test sevenzip2-4.1.$n {create simple archive with bool property (7z)} -constraints have7zip -setup {
        set f [file join [temporaryDirectory] sevenzip2.7z]
        set i1 [file join [temporaryDirectory] sevenzip2-1.txt]
        set i2 [file join [temporaryDirectory] sevenzip2-2.txt]
        writeFile $i1 $i1
        writeFile $i2 $i2
        set l [list $i1 $i2]
        set z ""
    } -cleanup {
        catch {rename $z ""}; unset z
        catch {file delete -force $f}; unset f
        catch {file delete -force $i1 $i2}; unset i1 i2
        unset l
    } -body {
        sevenzip create -properties [list s $v] -forcetype 7z $f $l
        set z [sevenzip open -forcetype 7z $f]
        dict get [$z info] solid
    } -result $r
    unset n p v r
}


unset updatableExtensions
cleanupTests
return
